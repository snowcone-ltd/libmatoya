// This Source Code Form is subject to the terms of the MIT License.
// If a copy of the MIT License was not distributed with this file,
// You can obtain one at https://spdx.org/licenses/MIT.html.

struct VS_OUTPUT {
	float4 position : SV_POSITION;
	float2 texcoord : TEXCOORD;
};

sampler tex0 : register(s0);
sampler tex1 : register(s1);
sampler tex2 : register(s2);

uniform float4 cb0 : register(c0); // width, height, vp_height
uniform float4 cb1 : register(c1); // effects, levels
uniform float4 cb2 : register(c2); // planes, rotation, conversion

inline float4 yuv_to_rgba(float conversion, float y, float u, float v)
{
	// 10-bit -> 16-bit
	if (conversion == 2.0 || conversion == 8.0) {
		y = y * 64.0;
		u = u * 64.0;
		v = v * 64.0;
	}

	// Full range
	if (conversion == 4.0 || conversion == 8.0) {
		u = u - 0.5;
		v = v - 0.5;

	// Limited
	} else {
		y = (y - 16.0 / 255.0) * (255.0 / 219.0);
		u = (u - 128.0 / 255.0) * (255.0 / 224.0);
		v = (v - 128.0 / 255.0) * (255.0 / 224.0);
	}

	float kr = 0.2126;
	float kb = 0.0722;

	float r = y + (2.0 - 2.0 * kr) * v;
	float b = y + (2.0 - 2.0 * kb) * u;
	float g = (y - kr * r - kb * b) / (1.0 - kr - kb);

	return float4(r, g, b, 1.0);
}

inline float4 sample_rgba(float planes, float conversion, float2 uv)
{
	if (planes == 2.0) {
		float y = tex2D(tex0, uv).r;
		float u = tex2D(tex1, uv).r;
		float v = tex2D(tex1, uv).a;

		return yuv_to_rgba(conversion, y, u, v);

	} else if (planes == 3.0) {
		float y = tex2D(tex0, uv).r;
		float u = tex2D(tex1, uv).r;
		float v = tex2D(tex2, uv).r;

		return yuv_to_rgba(conversion, y, u, v);

	} else if (conversion != 0.0) {
		float y = tex2D(tex0, uv).r;
		float u = tex2D(tex0, uv).g;
		float v = tex2D(tex0, uv).b;

		return yuv_to_rgba(conversion, y, u, v);

	} else {
		return tex2D(tex0, uv);
	}
}

inline void sharpen(float w, float h, float level, inout float2 uv)
{
	float2 res = float2(w, h);
	float2 p = uv * res;
	float2 c = floor(p) + 0.5;
	float2 dist = p - c;

	if (level >= 0.5) {
		dist = 16.0 * dist * dist * dist * dist * dist;

	} else {
		dist = 4.0 * dist * dist * dist;
	}

	uv = (c + dist) / res;
}

inline void scanline(float y, float h, float level, inout float4 rgba)
{
	float n = floor(h / 240.0);

	if (fmod(floor(y * h), n) < n / 2.0)
		rgba *= level;
}

inline float2 rotate(float rotation, float2 texcoord)
{
	float2 uv = texcoord;

	// Rotation
	if (rotation == 1.0 || rotation == 3.0) {
		float tmp = uv[0];
		uv[0] = uv[1];
		uv[1] = tmp;
	}

	// Flipped vertically
	if (rotation == 1.0 || rotation == 2.0)
		uv[1] = 1.0 - uv[1];

	// Flipped horizontally
	if (rotation == 2.0 || rotation == 3.0)
		uv[0] = 1.0 - uv[0];

	return uv;
}

float4 main(VS_OUTPUT input) : SV_TARGET
{
	// Uniforms
	float width = cb0[0];
	float height = cb0[1];
	float vp_height = cb0[2];

	float2 effects = float2(cb1[0], cb1[1]);
	float2 levels = float2(cb1[2], cb1[3]);

	float planes = cb2[0];
	float rotation = cb2[1];
	float conversion = cb2[2];

	// Rotate
	float2 uv = rotate(rotation, input.texcoord);

	// Sharpen
	for (uint x = 0; x < 2; x++)
		if (effects[x] == 2.0)
			sharpen(width, height, levels[x], uv);

	// Sample
	float4 rgba = sample_rgba(planes, conversion, uv);

	// Effects
	for (uint y = 0; y < 2; y++)
		if (effects[y] == 1.0)
			scanline(input.texcoord.y, vp_height, levels[y], rgba);

	return rgba;
}
