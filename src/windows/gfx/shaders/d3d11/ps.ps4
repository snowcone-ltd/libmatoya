// This Source Code Form is subject to the terms of the MIT License.
// If a copy of the MIT License was not distributed with this file,
// You can obtain one at https://spdx.org/licenses/MIT.html.

struct VS_OUTPUT {
	float4 position : SV_POSITION;
	float2 texcoord : TEXCOORD;
};

cbuffer VS_CONSTANT_BUFFER : register(b0) {
	float width;
	float height;
	float vp_height;
	float pad0;
	uint4 effects;
	float4 levels;
	uint format;
	uint rotation;
	uint2 pad1;
};

SamplerState ss {
};

Texture2D tex0 : register(t0);
Texture2D tex1 : register(t1);
Texture2D tex2 : register(t2);

inline float4 yuv_to_rgba(float y, float u, float v)
{
	// Using "RGB to YCbCr color conversion for HDTV" (ITU-R BT.709)

	y = (y - 0.0625) * 1.164;
	u = u - 0.5;
	v = v - 0.5;

	float r = y + 1.793 * v;
	float g = y - 0.213 * u - 0.533 * v;
	float b = y + 2.112 * u;

	return float4(r, g, b, 1.0);
}

inline float4 sample_rgba(uint format, float2 uv)
{
	// NV12, NV16
	if (format == 2 || format == 5) {
		float y = tex0.Sample(ss, uv).r;
		float u = tex1.Sample(ss, uv).r;
		float v = tex1.Sample(ss, uv).g;

		return yuv_to_rgba(y, u, v);

	// I420, I444
	} else if (format == 3 || format == 4) {
		float y = tex0.Sample(ss, uv).r;
		float u = tex1.Sample(ss, uv).r;
		float v = tex2.Sample(ss, uv).r;

		return yuv_to_rgba(y, u, v);

	// AYUV
	} else if (format == 8) {
		float y = tex0.Sample(ss, uv).r;
		float u = tex0.Sample(ss, uv).g;
		float v = tex0.Sample(ss, uv).b;

		return yuv_to_rgba(y, u, v);

	// BGRA
	} else {
		return tex0.Sample(ss, uv);
	}
}

inline void sharpen(float w, float h, float level, inout float2 uv)
{
	float2 res = float2(w, h);
	float2 p = uv * res;
	float2 c = floor(p) + 0.5;
	float2 dist = p - c;

	if (level >= 0.5) {
		dist = 16.0 * dist * dist * dist * dist * dist;

	} else {
		dist = 4.0 * dist * dist * dist;
	}

	uv = (c + dist) / res;
}

inline void scanline(float y, float h, float level, inout float4 rgba)
{
	float n = floor(h / 240.0);

	if (fmod(floor(y * h), n) < n / 2.0)
		rgba *= level;
}

inline float2 rotate(uint rotation, float2 texcoord)
{
	float2 uv = texcoord;

	// Rotation
	if (rotation == 1 || rotation == 3) {
		float tmp = uv[0];
		uv[0] = uv[1];
		uv[1] = tmp;
	}

	// Flipped vertically
	if (rotation == 1 || rotation == 2)
		uv[1] = 1.0 - uv[1];

	// Flipped horizontally
	if (rotation == 2 || rotation == 3)
		uv[0] = 1.0 - uv[0];

	return uv;
}

float4 main(VS_OUTPUT input) : SV_TARGET
{
	// Rotate
	float2 uv = rotate(rotation, input.texcoord);

	// Sharpen
	for (uint x = 0; x < 2; x++)
		if (effects[x] == 2)
			sharpen(width, height, levels[x], uv);

	// Sample
	float4 rgba = sample_rgba(format, uv);

	// Effects
	for (uint y = 0; y < 2; y++)
		if (effects[y] == 1)
			scanline(input.texcoord.y, vp_height, levels[y], rgba);

	return rgba;
}
